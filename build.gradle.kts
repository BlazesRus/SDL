// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {
    repositories {
        google()
        mavenCentral()
    }
    dependencies {
        classpath("com.android.tools.build:gradle:7.0.1")
        classpath("org.jetbrains.kotlin:kotlin-gradle-plugin:1.5.30")

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

allprojects {
    group = "org.libsdl"
    version = determineVersion()
    description = "Simple DirectMedia Layer"
    repositories {
        google()
        mavenCentral()
    }
}

tasks {
    wrapper {
        distributionType = Wrapper.DistributionType.ALL
    }
    register<Delete>("clean") {
        // Clean the sub-dirs generated by the Gradle build system, but keep the buildDir
        project.buildDir
            .listFiles { _, name -> name == "intermediates" || name == "kotlin" }
            ?.let { delete = it.toSet() }
    }
    register<Delete>("cleanAll") {
        dependsOn("clean")
    }
    register("version") {
        doLast {
            println(determineVersion())
        }
    }
}

fun determineVersion(): String {
    val file = project.file("include/SDL_version.h")
    val defines = Regex("^#define (?:SDL_MAJOR_VERSION|SDL_MINOR_VERSION|SDL_PATCHLEVEL)")
    val whitespaces = Regex("\\s+")
    return file.readLines()
        .filter { it.contains(defines) }
        .joinToString(".") { it.split(whitespaces).last() }
}
